plugins {
    id 'java'
}

java {
    sourceCompatibility(JavaVersion.VERSION_17)
    targetCompatibility(JavaVersion.VERSION_17)
}

configurations {
    configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
}

dependencies {
    implementation platform('com.sangjun.platform:product-platform')
    implementation('com.sangjun.common:common-utils')
    testImplementation platform('com.sangjun.platform:test-platform')

    implementation('org.springframework.boot:spring-boot-starter-log4j2')

    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    if (!name.endsWith("domain-core")) {
        if (!name.endsWith("application")) {
            def mapstructDep = implementation 'org.mapstruct:mapstruct:1.5.3.Final'
            annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructDep.getVersion()}"
            annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
            testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructDep.getVersion()}"
        }

        def lombokDep = compileOnly 'org.projectlombok:lombok:1.18.26'
        annotationProcessor "org.projectlombok:lombok:${lombokDep.getVersion()}"
        testCompileOnly "org.projectlombok:lombok:${lombokDep.getVersion()}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokDep.getVersion()}"
    }
}

test {
    useJUnitPlatform()
}
